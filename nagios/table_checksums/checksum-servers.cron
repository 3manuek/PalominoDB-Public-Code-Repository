#!/usr/bin/perl
use strict;
use warnings;
use Pod::Usage;
use YAML::Syck;
use Getopt::Long;
use File::Temp qw/ :mktemp /;

use constant OK => 0;
use constant WARNING => 1;
use constant CRITICAL => 2;
use constant UNKNOWN => 3;

my $cfgfile = undef;
my $mk_table_checksum = "/usr/bin/mk-table-checksum";
my $noop;
my $debug;
my $result_str = "";
my $result = OK;

GetOptions(
  "help|h" => sub { pod2usage() },
  "cfgfile=s" => \$cfgfile,
  "debug" => \$debug,
  "noop"  => \$noop
);

if(not defined $cfgfile) {
  pod2usage(-message => "Need --cfgfile!");
}

my $yml = YAML::Syck::LoadFile($cfgfile);
$mk_table_checksum = $yml->{'mktablechecksum'} if( exists $yml->{'mktablechecksum'} );
my $tables = join(",", @{$yml->{'tables'}});
my $servers = join(" ", @{$yml->{'servers'}});
my $nagios = $yml->{'nagios'};
my $user = $yml->{'user'};
my $pass = $yml->{'pass'};

if($debug or $noop) {
  use Data::Dumper;
  print Dumper($yml);
  print("$mk_table_checksum --checksum --user $user --pass $pass --tables $tables $servers\n"); 
}

unless($noop) {
  my @r = qx($mk_table_checksum --user $user --pass $pass --tables $tables $servers);

  if(($? >> 8) != 0) {
    system("echo -e '$nagios->{'svchost'}\\t$nagios->{'svcdesc'}\\t3\\tmk-table-checksum had an unknown issue.' |$nagios->{'send_nsca'} -H $nagios->{'nagioshost'} -c $nagios->{'nscaconfig'}");
    exit(0);
  }

  # Loop over tables, and check for the bad.
  my %tbls;
  foreach my $l (@r) {
    next if($l =~ /^DATABASE\s+TABLE/);
    my ($db, $table, $chunk, $host, $engine, $count, $csum, $time, $wait, $stat, $lag) = split /\s+/, $l;
    my $db_table = "$db.$table";
    if(not exists $tbls{$db_table}) {
      $tbls{$db_table} = $csum;
    }
    elsif($tbls{$db_table} != $csum) {
      $result_str .= "$db_table BAD on $host; $result_str";
      $result = CRITICAL;
    }
    else {
      $result_str .= "; $db_table OK";
    }
  }
  $result_str =~ s/^; //;

  if($result == OK) {
    $result_str = "OK: $result_str";
  }
  else {
    $result_str = "CRITICAL: $result_str";
  }
}

if($debug or $noop) {
  print("echo -e '$nagios->{'svchost'}\\t$nagios->{'svcdesc'}\\t$result\\t$result_str' |$nagios->{'send_nsca'} -H $nagios->{'nagioshost'} -c $nagios->{'nscaconfig'}");
}
unless($noop) {
  system("echo -e '$nagios->{'svchost'}\\t$nagios->{'svcdesc'}\\t$result\\t$result_str' |$nagios->{'send_nsca'} -H $nagios->{'nagioshost'} -c $nagios->{'nscaconfig'}");
}

__END__
=head1 NAME

checksum-servers.cron - Runs mk-table-checksum from cron.

=head1 SYNOPSIS

0 */8 * * * checksum-servers.cron --cfgfile csum-servers.yml

Options:

   --help      This help
   --cfgfile   YAML file of tables and IPs, and other configuration.
   --debug     Print status of what it's doing.
   --noop      Don't do anything.
