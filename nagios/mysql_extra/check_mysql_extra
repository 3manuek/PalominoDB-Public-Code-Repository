#! /usr/bin/perl
# <one line to give the program's name and a brief idea of what it does.>
# Copyright (C) 2009-2012 PalominoDB, Inc.
# 
# You may contact the maintainers at eng@palominodb.com.
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
# 
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
# 
# You should have received a copy of the GNU General Public License along
# with this program; if not, write to the Free Software Foundation, Inc.,
# 51 Franklin Street, Fifth Floor, Boston, MA 02110-1301 USA.

use strict;
use warnings FATAL => 'all';
use Getopt::Long qw(:config no_ignore_case);
use Time::HiRes qw(time);
use DBI;
use Data::Dumper;

# Define nagios constants
use constant OK       => 0;
use constant WARNING  => 1;
use constant CRITICAL => 2;
use constant UNKNOWN  => 3;

use constant TIMEOUT => 10;

sub print_help(;$);
sub check_connections ($$$$$$$);
sub check_slow_queries ($$$$$$$$);
sub print_slow_queries (@);

my %o;

sub print_help(;$) {
  my $err = ($_[0] ? "Error: $_[0]" : "");
  print <<EOF;
Copyright (c) 2009-2010, PalominoDB, Inc.

MySQL Extended Monitoring check plugin for Nagios

Usage: $0 -H <host> [options]

Options:
  -H, --hostname=STRING
     Name or IP address of host to check
  -w, --connections-warning=INTEGER
    Maximum number of connections before a warning state is triggered
  -c, --connections-critical=INTEGER
    Maximum number of connections before a critical state is triggered
  -W, --slowquery-warning=INTEGER
    Maximum number of seconds before a warning state is triggered
  -C, --slowquery-critical=INTEGER
    Maximum number of seconds before a critical state is triggered
  -s, --show-slow-queries
    Actually display running slow queries
  -t, --timeout=INTEGER
    Number of seconds before plugin times out (default: 15)
  -P, --port=INTEGER
    Port on which the MySQL server is running on <host> (Default: 3306)
  -u, --user=STRING
    Username with which to connect (Default: root)
  -p, --password=STRING
    Password with which to connect (Default: (blank))
  -d, --database=STRING
    Database name for connection
  -v, --verbose
    Print extra debugging information
  -V, --version
     Show version information

$err
EOF
  exit(UNKNOWN);
}

MAIN:
{
    my (
        $connections_warning, $connections_critical, $slowquery_warning,
        $slowquery_critical,  $show_slow_queries
    );
    my ($mode_selected, $TIMEOUT) = (0, TIMEOUT);

    GetOptions(\%o,
        "help|h",
        "verbose|v",
        "timeout|t=i",
        "connections-warning|w=i",
        "connections-critical|c=i",
        "slowquery-warning|W=i",
        "slowquery-critical|C=i",
        "show-slow-queries|s",
        "port|P=i",
        "user|u=s",
        "password|p=s",
        "database|d=s",
        "hostname|H=s"
    );

    if ($o{'help'}) {
        print_help();
        exit OK;
    }

    ($o{'hostname'}) || print_help("Hostname / IP address not specified.");
    print_help("Invalid host: $o{'hostname'}") unless ( $o{'hostname'} =~ /([-.A-Za-z0-9]+)/ );
    my $host = $o{'hostname'};

    $mode_selected = 0;

    if ($o{'connections-warning'}) {
        ++$mode_selected;
        print_help("Invalid connections warning threshold: $o{'connections-warning'}\n")
          unless ( $o{'connections-warning'} =~ /^([0-9]+)$/ );
        $connections_warning = $o{'connections-warning'};
    }

    if ($o{'connections-critical'}) {
        ++$mode_selected;
        print_help("Invalid connections critical threshold: $o{'connections-critical'}\n")
          unless ( $o{'connections-critical'} =~ /^([0-9]+)$/ );
        $connections_critical = $o{'connections-critical'};
    }

    if ($o{'slowquery-warning'}) {
        ++$mode_selected;
        print_help("Invalid slow query warning threshold: $o{'slowquery-warning'}")
          unless ( $o{'slowquery-warning'} =~ /^([0-9]+)$/ );
        $slowquery_warning = $o{'slowquery-warning'};
    }

    if ($o{'slowquery-critical'}) {
        ++$mode_selected;
        print_help("Invalid slow query critical threshold: $o{'slowquery-critical'}")
          unless ( $o{'slowquery-critical'} =~ /^([0-9]+)$/ );
        $slowquery_critical = $o{'slowquery-critical'};
    }

    if ($o{'show-slow-queries'}) {
        $show_slow_queries = 1;
    }
    else {
        $show_slow_queries = 0;
    }

    ($o{'port'}) || ( $o{'port'} = 3306 );
    print_help("Invalid port: $o{'port'}") unless ( $o{'port'} =~ /^([0-9]+)$/ );
    my $port = $o{'port'};

    ($o{'user'}) || ( $o{'user'} = 'root' );
    my $user = $o{'user'};

    ($o{'password'}) || ( $o{'password'} = '' );
    my $password = $o{'password'};

    ($o{'database'}) || ( $o{'database'} = '' );
    my $db = $o{'database'};

    if ( $o{'timeout'} && $o{'timeout'} =~ /^([0-9]+)$/ ) { $TIMEOUT = $1; }

    if ( $mode_selected > 2 ) {
        print_help("Only one set of critical / warning options can be specified.\n");
    }

    if ( defined($connections_warning) && !defined($connections_critical) ) {
        print_help("Both warning and critical options must be specified.");
    }

    if ( defined($slowquery_warning) && !defined($slowquery_critical) ) {
        print_help("Both warning and critical options must be specified.");
    }

    # Don't risk hanging nagios.
    $SIG{'ALRM'} = sub {
        print "TIMEOUT after $TIMEOUT seconds.\n";
        exit UNKNOWN;
    };
    alarm($TIMEOUT);

    if ( defined($connections_warning) ) {
        check_connections( $host, $port, $user, $password, $db,
            $connections_warning, $connections_critical );
    }
    elsif ( defined($slowquery_warning) ) {
        check_slow_queries( $host, $port, $user, $password, $db,
            $slowquery_warning, $slowquery_critical, $show_slow_queries );
    }

    print "No action.\n";
    exit UNKNOWN;
}

sub check_connections ($$$$$$$) {
    my ( $host, $port, $user, $password, $db, $warning, $critical ) = @_;
    my ( $rows, $row, $statement );

    my $source = "DBI:mysql:database=$db:host=$host:port=$port";
    my $dbh = DBI->connect( $source, $user, $password );
    unless ($dbh) {
        print "Failed to connect to $source.\n";
        exit CRITICAL;
    }

    $statement = $dbh->prepare("SHOW FULL PROCESSLIST");
    unless ($statement) {
        print "Failed to prepare statement.\n";
        exit CRITICAL;
    }

    unless ( $statement->execute() ) {
        print "Failed to execute statement.\n";
        exit CRITICAL;
    }

    my $sleepers       = 0;
    my $queries        = 0;
    my $binlog         = 0;
    my $delayedInserts = 0;
    for ( $rows = 0 ; ( $row = $statement->fetchrow_hashref() ) ; ++$rows ) {
        if ( $row->{Command} eq "Query" ) {
            ++$queries;
        }
        elsif ( $row->{Command} eq "Sleep" ) {
            ++$sleepers;
        }
        elsif ( $row->{Command} eq "Binlog Dump" ) {
            ++$binlog;
        }
        elsif ( $row->{Command} eq "Delayed insert" ) {
            ++$delayedInserts;
        }
    }

    if ( $rows >= $critical ) {
        printf
"MySQL Connections CRITICAL - %s connection%s (%d quer%s, %d sleeping, %d binlog, %d delayed insert%s).\n",
          $rows,
          $rows != 1 ? "s" : "",
          $queries,
          $queries != 1 ? "ies" : "y",
          $sleepers,
          $binlog,
          $delayedInserts,
          $delayedInserts != 1 ? "s" : "";
        exit CRITICAL;
    }
    elsif ( $rows >= $warning ) {
        printf
"MySQL Connections WARNING - %s connection%s (%d quer%s, %d sleeping, %d binlog, %d delayed insert%s).\n",
          $rows,
          $rows != 1 ? "s" : "",
          $queries,
          $queries != 1 ? "ies" : "y",
          $sleepers,
          $binlog,
          $delayedInserts,
          $delayedInserts != 1 ? "s" : "";
        exit WARNING;
    }

    printf
"MySQL Connections OK - %s connection%s (%d quer%s, %d sleeping, %d binlog, %d delayed insert%s).\n",
      $rows,
      $rows != 1 ? "s" : "",
      $queries,
      $queries != 1 ? "ies" : "y",
      $sleepers,
      $binlog,
      $delayedInserts,
      $delayedInserts != 1 ? "s" : "";
    exit OK;
}

sub check_slow_queries ($$$$$$$$) {
    my ( $host, $port, $user, $password, $db, $warning, $critical, $show ) = @_;
    my ( $rows, $row, $statement );

    my $source = "DBI:mysql:database=$db:host=$host:port=$port";
    my $dbh = DBI->connect( $source, $user, $password );
    unless ($dbh) {
        print "Failed to connect to $source.\n";
        exit CRITICAL;
    }

    $statement = $dbh->prepare("SHOW FULL PROCESSLIST");
    unless ($statement) {
        print "Failed to prepare statement.\n";
        exit CRITICAL;
    }

    unless ( $statement->execute() ) {
        print "Failed to execute statement.\n";
        exit CRITICAL;
    }

    my $criticalQueries = 0;
    my $warningQueries  = 0;
    my $maxTime         = 0;
    my @slowQueries;

    while ( $row = $statement->fetchrow_hashref() ) {
        if ( $row->{Command} eq "Query" ) {
            if ( $row->{Time} >= $critical ) {
                ++$criticalQueries;
                push( @slowQueries, $row );
            }
            elsif ( $row->{Time} >= $warning ) {
                ++$warningQueries;
                push( @slowQueries, $row );
            }

            if ( $row->{Time} > $maxTime ) {
                $maxTime = $row->{Time};
            }
        }
    }

    my $total = ( $criticalQueries + $warningQueries );

    #FIXME: Log details of slow queries.
    if ( $criticalQueries > 0 ) {
        printf
"MySQL Slow Queries CRITICAL - %s slow quer%s (%d quer%s over %ds, %d quer%s over %ds; max: %d seconds).\n",
          $total,
          $total != 1 ? "ies" : "y",
          $warningQueries,
          $warningQueries != 1 ? "ies" : "y",
          $warning,
          $criticalQueries,
          $criticalQueries != 1 ? "ies" : "y",
          $critical,
          $maxTime;
        print_slow_queries(@slowQueries);
        exit CRITICAL;
    }
    elsif ( $warningQueries > 0 ) {
        printf
"MySQL Slow Queries WARNING - %s slow quer%s (%d quer%s over %ds, %d quer%s over %ds; max: %d seconds).\n",
          $total,
          $total != 1 ? "ies" : "y",
          $warningQueries,
          $warningQueries != 1 ? "ies" : "y",
          $warning,
          $criticalQueries,
          $criticalQueries != 1 ? "ies" : "y",
          $critical,
          $maxTime;
        print_slow_queries(@slowQueries);
        exit WARNING;
    }

    printf
"MySQL Slow Queries OK - 0 slow queries (0 queries over %ds, 0 queries over %ds; max: %d seconds).\n",
      $warning,
      $critical,
      $maxTime;
    exit OK;
}

sub print_slow_queries (@) {
    my (@slowQueries) = @_;

    foreach my $query (@slowQueries) {
        $query->{Info} =~ s/\n/ /gs;
        printf "#%d [%s] (%ds): %s\n", $query->{Id}, $query->{User},
          $query->{Time}, $query->{Info};
    }
}
